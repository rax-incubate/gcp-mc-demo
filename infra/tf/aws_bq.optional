
locals {
  env2_bq_data_bucket = "bq-test-gfytoj"
}

resource "aws_s3_bucket" "bq_data" {
  bucket = local.env2_bq_data_bucket
}

resource "google_bigquery_connection" "bq_omni" {
  project = var.env1_gcp_project
    connection_id = "bq-omni-aws-connection"
    friendly_name = "bq-omni-aws-connection"

    location      = "aws-${var.env2_aws_default_region}"
    aws {
        access_role {
        # This must be constructed as a string instead of referencing the AWS resources
        # directly to avoid a resource dependency cycle in Terraform.
        iam_role_id = "arn:aws:iam::${data.aws_caller_identity.current.account_id}:role/bq-omni-connection"
        }
    }
}


resource "aws_iam_policy" "bq_omni" {
    name = "bq-omni-connection-policy"

    policy = <<-EOF
            {
                "Version": "2012-10-17",
                "Statement": [
                    {
                        "Sid": "BucketLevelAccess",
                        "Effect": "Allow",
                        "Action": ["s3:ListBucket"],
                        "Resource": ["arn:aws:s3:::${aws_s3_bucket.bq_data.id}"]
                    },
                    {
                        "Sid": "ObjectLevelAccess",
                        "Effect": "Allow",
                        "Action": ["s3:GetObject"],
                        "Resource": [
                            "arn:aws:s3:::${aws_s3_bucket.bq_data.id}",
                            "arn:aws:s3:::${aws_s3_bucket.bq_data.id}/*"
                            ]
                    }
                ]
            }
            EOF
}


resource "aws_iam_role" "bq_omni" {
    name                 = "bq-omni-connection"
    max_session_duration = 43200

    assume_role_policy = <<-EOF
        {
        "Version": "2012-10-17",
        "Statement": [
            {
            "Effect": "Allow",
            "Principal": {
                "Federated": "accounts.google.com"
            },
            "Action": "sts:AssumeRoleWithWebIdentity",
            "Condition": {
                "StringEquals": {
                "accounts.google.com:sub": "${google_bigquery_connection.bq_omni.aws[0].access_role[0].identity}"
                }
            }
            }
        ]
        }
        EOF
}

resource "aws_iam_role_policy_attachment" "bq_omni" {
    role       = aws_iam_role.bq_omni.name
    policy_arn = aws_iam_policy.bq_omni.arn
}


